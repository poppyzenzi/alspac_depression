title: 
latent class analysis
for wide format alspac 
depression data (up to t=4)

data:
    FILE IS alspac_smfq_wide.txt;

variable:
    NAMES ARE Subject Sex mfq_t01 mfq_t02 mfq_t03 mfq_t04 
    mfq_t05 mfq_t06 mfq_t07 mfq_t08 mfq_t09 mfq_t10 mfq_t11 
    age_t01 age_t02 age_t03 age_t04 age_t05 age_t06 age_t07 
    age_t08 age_t09 age_t10 age_t11;
    missing are all (99); 
    USEVARIABLES ARE mfq_t01 mfq_t02 mfq_t03 mfq_t04;
    AUXILIARY Subject;
    classes = c (4);

analysis: 
	processors = 4 (starts);
	type = mixture;
	starts = 3500 350;
	!stiterations = 25;
	!stscale = 25;


MODEL:
    %overall%
    i by mfq_t01@1 mfq_t02@1 mfq_t03@1 mfq_t04@1;
    
    s by mfq_t01@0 mfq_t02@1 mfq_t03@2 mfq_t04@3;
    
    q by mfq_t01@0 mfq_t02@1 mfq_t03@4 mfq_t04@9;
    
    
    [mfq_t01@0 mfq_t02@0 mfq_t03@0 mfq_t04@0];
    
    mfq_t01 mfq_t02 mfq_t03 mfq_t04 (eqresvar);
    [i s q];
    i s q@0;
    i with s;
    i with q@0;
    s with q@0;
    
  %c#1% 
  [i s q]; 
  
  %c#2% 
  [i s q]; 
  
  %c#3% 
  [i s q]; 
  
  %c#4% 
  [i s q]; 
 



OUTPUT: tech1 tech4 tech11;



PLOT: type is plot3;
  series = mfq_t01 mfq_t02 mfq_t03 mfq_t04 (s);
  

Savedata: file is 4_class_probs.txt;
   save = cprobabilities;
 
